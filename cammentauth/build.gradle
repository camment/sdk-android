apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    resourcePrefix 'cmmauth_'

    def RELEASE_VERSION = 2

    def MAJOR_VERSION = 1

    def MINOR_VERSION = 2

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionName RELEASE_VERSION + "." + MAJOR_VERSION + "." + MINOR_VERSION
        versionCode RELEASE_VERSION * 100000000 + 10000 * MAJOR_VERSION + 100 * MINOR_VERSION

        defaultPublishConfig 'release'
        publishNonDefault true
    }

    buildTypes {
        debug {
            minifyEnabled false
            useProguard false
            debuggable true
        }

        release {
            minifyEnabled false
            useProguard false
        }
    }

    productFlavors {
        devApi {

        }

        prodApi {

        }
    }
}

configurations {
    devApiDebugCompile
    prodApiDebugCompile
    devApiReleaseCompile
    prodApiReleaseCompile
}

ext {
    facebookVersion = '4.25.0'

    facebookDependencies = [
            facebook: "com.facebook.android:facebook-android-sdk:${facebookVersion}",
    ]
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile facebookDependencies.values()

    devApiDebugCompile project(path: ':cammentsdk', configuration: 'devApiDebug')
    prodApiDebugCompile project(path: ':cammentsdk', configuration: 'prodApiDebug')
    devApiReleaseCompile project(path: ':cammentsdk', configuration: 'devApiRelease')
    prodApiReleaseCompile project(path: ':cammentsdk', configuration: 'prodApiRelease')
}

publishing {

    def RELEASE_VERSION = 2

    def MAJOR_VERSION = 1

    def MINOR_VERSION = 2

    publications {
        maven(MavenPublication) {
            groupId 'tv.camment.cammentauth'
            artifactId 'cammentauth'
            version RELEASE_VERSION + "." + MAJOR_VERSION + "." + MINOR_VERSION
            artifact "$buildDir/outputs/aar/cammentauth-prodApi-release.aar"
            //aar artifact you want to publish

            //generate pom nodes for dependencies
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.compile.allDependencies.each { dependency ->
                    if (dependency.name != "unspecified") {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }
            }
        }
    }

    //publish to filesystem repo
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}
