apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    resourcePrefix 'cmmsdk_'

    def RELEASE_VERSION = 2

    def MAJOR_VERSION = 0

    def MINOR_VERSION = 0

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionName RELEASE_VERSION + "." + MAJOR_VERSION + "." + MINOR_VERSION
        versionCode RELEASE_VERSION * 100000000 + 10000 * MAJOR_VERSION + 100 * MINOR_VERSION

        defaultPublishConfig 'release'
        publishNonDefault true

        buildConfigField("boolean", "USE_DEEPLINK", "true")
        buildConfigField("boolean", "SHOW_GROUP_LIST", "false")
        buildConfigField("boolean", "SHOW_ADS", "true")
        buildConfigField("boolean", "USE_MIXPANEL", "false")
    }

    buildTypes {
        debug { //PROD
            minifyEnabled false
            useProguard false
            debuggable true

            buildConfigField("String", "ENVIRONMENT", "\"prod\"")
        }

        debugDev { //DEV
            minifyEnabled false
            useProguard false
            debuggable true

            buildConfigField("String", "ENVIRONMENT", "\"dev\"")
        }

        release { //PROD
            minifyEnabled true
            useProguard true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            consumerProguardFiles getDefaultProguardFile('proguard-android.txt'), getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField("String", "ENVIRONMENT", "\"prod\"")
        }

        releaseDev { //DEV
            minifyEnabled true
            useProguard true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            consumerProguardFiles getDefaultProguardFile('proguard-android.txt'), getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField("String", "ENVIRONMENT", "\"dev\"")
        }
    }
}

ext {
    supportLibVersion = rootProject.ext.supportLibVersion
    awsVersion = '2.6.13'
    constraintLayoutVersion = '1.0.2'
    exoPlayerVersion = 'r2.5.4'
    glideVersion = '4.3.0'
    easyPermissionsVersion = '0.4.2'
    eventBusVersion = '3.0.0'
    mixpanelVersion = '5.2.1'

    awsDependencies = [
            core                   : "com.amazonaws:aws-android-sdk-core:${awsVersion}",
            cognitoSync            : "com.amazonaws:aws-android-sdk-cognito:${awsVersion}",
            cognitoIdentityProvider: "com.amazonaws:aws-android-sdk-cognitoidentityprovider:${awsVersion}",
            apiGatewayCore         : "com.amazonaws:aws-android-sdk-apigateway-core:${awsVersion}",
            iot                    : "com.amazonaws:aws-android-sdk-iot:${awsVersion}",
            analytics              : "com.amazonaws:aws-android-sdk-mobileanalytics:${awsVersion}",
    ]

    otherDependencies = [
            supportv4       : "com.android.support:support-v4:${supportLibVersion}",
            design          : "com.android.support:design:${supportLibVersion}",
            recyclerView    : "com.android.support:recyclerview-v7:${supportLibVersion}",
            constraintLayout: "com.android.support.constraint:constraint-layout:${constraintLayoutVersion}",
            exoPlayer       : "com.google.android.exoplayer:exoplayer:${exoPlayerVersion}",
            easyPermissions : "pub.devrel:easypermissions:${easyPermissionsVersion}",
            eventBus        : "org.greenrobot:eventbus:${eventBusVersion}",
    ]

    glideDependencies = [
            glide        : "com.github.bumptech.glide:glide:${glideVersion}",
            glideCompiler: "com.github.bumptech.glide:compiler:${glideVersion}",
    ]

    mixpanelDependencies = [
            mixpanel: "com.mixpanel.android:mixpanel-android:${mixpanelVersion}",
    ]
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile awsDependencies.values()
    compile otherDependencies.values()

    compile glideDependencies.glide
    annotationProcessor glideDependencies.glideCompiler

    compile mixpanelDependencies.values()
}

publishing {

    def RELEASE_VERSION = 2

    def MAJOR_VERSION = 0

    def MINOR_VERSION = 0

    publications {
        maven(MavenPublication) {
            groupId 'tv.camment.cammentsdk'
            artifactId 'cammentsdk'
            version RELEASE_VERSION + "." + MAJOR_VERSION + "." + MINOR_VERSION
            artifact "$buildDir/outputs/aar/cammentsdk-release.aar"
            //aar artifact you want to publish

            //generate pom nodes for dependencies
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.compile.allDependencies.each { dependency ->
                    if (dependency.name != "unspecified") {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }
            }
        }
    }

    //publish to filesystem repo
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}